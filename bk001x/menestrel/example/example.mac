; Простая тестовая программа для "Менестреля"
; Выводит тон 1 кГц на 0-м канале (пин 10) DD1
; Остальные каналы притянуты к земле
; Код собирается командой: pdpy11 example.mac
; Александр Алексеев, 2023

    MOV    #0xE3FF, R0        ; Инициализация:
    MOV    R0, @#256          ;   на /WR высокое напряжение
                              ;   DD1 и DD2 не выбраны (высокие напряжения на /CS)
    MOV    R0, @#177714       ;   остальные напряжения низкие
    CALL   DELAY

    CALL   SET_A0_HIGH        ; Настройка, высокий уровень на A0 и A1
    CALL   SET_A1_HIGH

    CALL   SELECT_DD2         ; Выбираем DD2
    CALL   DELAY

    MOV    #0b00110000, R0    ; 0-ые режимы для каналов 0..2
    CALL   SETUP_CHANNEL      ; в 0-ом режиме без установки счетчика выход низкий 
    MOV    #0b01110000, R0
    CALL   SETUP_CHANNEL      
    MOV    #0b10110000, R0
    CALL   SETUP_CHANNEL

    CALL   UNSELECT_DD2       ; Выбираем DD1
    CALL   SELECT_DD1         
    CALL   DELAY

    ; D7, D6     = 0b00,  канал 0
    ; D5, D4     = 0b11,  загрузка 16-и битного слова (little-endian)
    ; D3, D2, D1 = 0bX11, режим работы 3
    ; D0         = 0b0,   двоичный счет
    MOV    #0b00110110, R0
    CALL   SETUP_CHANNEL   
    MOV    #0b01110000, R0    ; 0-ые режимы для каналов 1 и 2
    CALL   SETUP_CHANNEL      ; в 0-ом режиме без установки счетчика выход низкий 
    MOV    #0b10110000, R0
    CALL   SETUP_CHANNEL   

    CALL   SET_A0_LOW         ; Выбираем 0-ый счетчик (пин 10)
    CALL   SET_A1_LOW
    CALL   DELAY
    MOV    #1000., R0         ; Частота меандра равна 1_000_000 / R0
    CALL   SEND_WORD          ; В данном случае 1 кГц

    CALL   UNSELECT_DD1       ; Можно не делать, но для порядку
    CALL   SET_GATE_HIGH      ; Включаем все каналы

    MOV    #MESSAGE, R1       ; Сообщаем пользователю, что программа работает
    CLR    R2                 ; Конец текста - нулевой байт
    EMT    20                 ; Вывод текста

0:
    BR 0                      ; Бесконечный цикл

; Настройка одного канала
; Перед вызовом на A0 и A1 нужно подать высокое напряжение 
SETUP_CHANNEL:
    CALL   SET_DATA
    CALL   WR_STROBE
    RET

; Посылка 16-и битного слова, записанного в R0
; Перед вызовом через A0 и A1 нужно выбрать соответствующий канал
SEND_WORD:
    CALL   SET_DATA
    CALL   WR_STROBE

    SWAB   R0
    CALL   SET_DATA
    CALL   WR_STROBE

    SWAB   R0
    RET

; Установка значений на входах D0..D7 (DO00..DO07)
; Вход: младший байт R0, бит 1 - высокое напряжение, бит 0 - низкое напряжение
;       значение старшего байта R0 ни на что не влияет
; Значение регистров после возврата - без изменений
SET_DATA:
    COMB   R0
    MOVB   R0, @#256
    MOV    @#256, @#177714
    COMB   R0
    RET

; Высокое напряжение на входах A0 (DO08)
SET_A0_HIGH:
    BIC    #0x0100, @#256
    MOV    @#256, @#177714
    RET

; Низкое напряжение на входах A0 (DO08)
SET_A0_LOW:
    BIS    #0x0100, @#256
    MOV    @#256, @#177714
    RET

; Высокое напряжение на входах A1 (DO09)
SET_A1_HIGH:
    BIC    #0x0200, @#256
    MOV    @#256, @#177714
    RET

; Низкое напряжение на входах A1 (DO09)
SET_A1_LOW:
    BIS    #0x0200, @#256
    MOV    @#256, @#177714
    RET

; Выбрать DD1 (низкое напряжение на /CS)
SELECT_DD1:
    BIS    #0x0400, @#256      ; /CS чипа DD1 - на DO10
    MOV    @#256, @#177714
    RET

; Снять выбор DD1 (высокое напряжение на /CS)
UNSELECT_DD1:
    BIC    #0x0400, @#256      ; /CS чипа DD1 - на DO10
    MOV    @#256, @#177714
    RET

; Выбрать DD2 (низкое напряжение на /CS)
SELECT_DD2:
    BIS    #0x0800, @#256      ; /CS чипа DD2 - на DO11
    MOV    @#256, @#177714
    RET

; Снять выбор DD2 (высокое напряжение на /CS)
UNSELECT_DD2:
    BIC    #0x0800, @#256      ; /CS чипа DD2 - на DO11
    MOV    @#256, @#177714
    RET

; Запись данных. Подаем на /WR (DO12) низкое напряжение, затем возвращаем высокое
WR_STROBE:
    PUSH   R0
    CALL   DELAY
    MOV    @#256, R0
    BIS    #0x1000, R0        ; низкое напряжение
    MOV    R0, @#177714
    CALL   DELAY
    BIC    #0x1000, R0        ; высокое напряжение
    MOV    R0, @#177714
    CALL   DELAY
    POP    R0
    RET

; Высокое напряжение на входах G0..G2 (DO15)
SET_GATE_HIGH:
    BIC    #0x8000, @#256
    MOV    @#256, @#177714
    RET

; Низкое напряжение на входах G0..G2 (DO15)
SET_GATE_LOW:
    BIS    #0x8000, @#256
    MOV    @#256, @#177714
    RET

; Задержка ~2 мс. Портит значение R0
; По даташиту 8253, 1 мс достаточно для всех переходных процессов
DELAY:
    MOV    #300., R0
0:
    SOB    R0, 0
    RET

MESSAGE:
  .BYTE 14                 ; Сброс экрана
  .ASCII "Простая демонстрация работы Менестреля."
  .BYTE 0x0A               ; Перевод строки
  .ASCII "Программа исполняется."
  .BYTE 0x0A               ; Перевод строки
  .BYTE 0x00               ; Конец ASCIZ-строки

make_wav