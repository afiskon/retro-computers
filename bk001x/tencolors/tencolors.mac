BEGIN:
    ; MOV     #0x00D8, @#177664    ; Что будет если использовать РП?
    MOV     #0x4000, R1          ; Начало экрана

COLOR_Y:                         ; Делим экран на 4 части по Y
    CMP     R1, #0x5000          ; Важен порядок операндов!
    BLO     1                    ; R1 беззнаково меньше 0x5000?
    CMP     R1, #0x6000
    BLO     2
    CMP     R1, #0x7000
    BLO     3
                                 ; 0x7000 <= Y < 0x8000
    MOV     #0x4444, R0          ; 0x4444 = 0b01000100... (синий)
    BR      COLOR_X
1:                               ; 0x4000 <= Y < 0x5000
    CLR     R0                   ; 0x0000 = 0b00000000... (черный)
    BR      COLOR_X
2:                               ; 0x5000 <= Y < 0x6000
    MOV     #0xCCCC, R0          ; 0xCCCC = 0b11001100... (красный)
    BR      COLOR_X
3:                               ; 0x6000 <= Y < 0x7000
    MOV     #0x8888, R0          ; 0x8888 = 0b10001000... (зеленый)

COLOR_X:                         ; Делим экран на 4 части по X
    MOV     R1, R2
    BIC     #0xFFCF, R2          ; 0xFFCF = COM(0x0030)
    CMP     #0x0030, R2
    BEQ     5
    CMP     #0x0020, R2
    BEQ     6
    CMP     #0x0010, R2
    BEQ     7
    BR      DRAW_WORD            ; оставляем нули (черный)
5:
    ADD     #0x1111, R0          ; 0x1111 = 0b00010001... (синий)
    BR      DRAW_WORD
6:
    ADD     #0x2222, R0          ; 0x2222 = 0b00100010... (зеленый)
    BR      DRAW_WORD
7:
    ADD     #0x3333, R0          ; 0x3333 = 0b00110011... (красный)

DRAW_WORD:

    BIT     #0x0040, R1          ; для каждой второй строки сдвигаем слово
    BNE     SKIP_SHIFT           ; чтобы получить "шашечки"

    ASL     R0                   ; циклический сдвиг R0 влево на 2 бита
    ADC     R0
    ASL     R0
    ADC     R0

SKIP_SHIFT:

    MOV     R0, (R1)+            ; Запись очередного слова в ОЗУ
    TST     R1                   ; Проверяем старший бит слова
    BPL     COLOR_Y              ; Если слово положительное, не дошли до 0x8000
    BR      BEGIN                ; Дошли до конца экрана. Программа зациклена
